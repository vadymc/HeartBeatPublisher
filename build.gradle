buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
		testContainersVersion = '1.10.1'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("net.researchgate:gradle-release:2.7.0")
	}
}

plugins {
	id "com.jfrog.bintray" version "1.8.4"
	id "maven-publish"
	id 'com.gradle.build-scan' version '1.16'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.researchgate.release'
apply plugin: 'maven'

group = 'vadc'
sourceCompatibility = 11

ext {
	bintrayUser = "$System.env.BINTRAY_USER"
	bintrayKey = "$System.env.BINTRAY_KEY"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework:spring-jms')
	implementation('com.amazonaws:aws-java-sdk:1.11.461')
	implementation('com.amazonaws:amazon-sqs-java-messaging-lib:1.0.4')
	implementation('com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.0')
	implementation('com.google.api-client:google-api-client:1.27.0')
	implementation('com.google.firebase:firebase-admin:6.6.0')
	compileOnly('org.projectlombok:lombok')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation("org.testcontainers:testcontainers:$testContainersVersion")
	testImplementation("org.testcontainers:localstack:$testContainersVersion")
	testImplementation('org.awaitility:awaitility:3.1.3')
}

//release
afterReleaseBuild.dependsOn bintrayUpload


def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "vadc"
			name "Vadym Chekrii"
			email "vadym.chekrii@gmail.com"
		}
	}

	scm {
		url "https://github.com/vadymc/HeartBeatPublisher"
	}
}

publishing {
	publications {
		HeartBeatFunnel(MavenPublication) {
			from components.java
			groupId 'vadc'
			artifactId 'HeartBeatPublisher'
			version "$project.version"
			pom.withXml {
				def root = asNode()
				root.appendNode('name', 'HeartBeat Publisher')
				root.appendNode('url', 'https://github.com/vadymc/HeartBeatPublisher')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = bintrayUser
	key = bintrayKey
	publications=['HeartBeatPublisher']
	publish = true
	pkg {
		repo = 'heartbeat'
		name = 'HeartBeatPublisher'
		userOrg = 'vadc'
		vcsUrl = 'https://github.com/vadymc/HeartBeatPublisher.git'
		licenses = ['Apache-2.0']
		version {
			name = "$project.version"
			desc = "$project.version"
			released  = new Date()
		}
	}
}

release {
	failOnUnversionedFiles = false
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}
